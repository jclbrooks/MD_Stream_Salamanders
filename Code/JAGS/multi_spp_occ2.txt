
    model {
    
    #PRIORS
    
    # Set species loop
    for(i in 1:n_species) {
    
    b0[i] ~ dnorm(mu.b0, tau.b0)
    b1[i] ~ dnorm(mu.b1, tau.b1)

    a0[i] ~ dnorm(mu.a0, tau.a0)
    a1[i] ~ dnorm(mu.a1, tau.a1)
    }
    
    #HYPERPRIORS
    mu.b0 ~ dnorm(0, 0.01)    
    tau.b0 ~ dgamma(0.1, 0.1)
    
    mu.b1 ~ dnorm(0, 0.01)
    tau.b1 ~ dgamma(0.1, 0.1)
    
    mu.a0 ~ dnorm(0, 0.01)
    tau.a0 ~ dgamma(0.1, 0.1)
    
    mu.a1 ~ dnorm(0, 0.01)
    tau.a1 ~ dgamma(0.1, 0.1)
    
# array transect (j) x visit (k) x species (i) x stage (l)
    # Estimation of Z matrix (true occurrence for species i at site j)	
    for(i in 1:n_species) {	
      for(j in 1:n_trans) { 
        for(l in 1:n_stages) {
    logit(psi[j,i,l]) <- b0[i] + b1[i]*ec[j]

    Z[j,i,l] ~ dbern(psi[j,i,l])
    
    # Estimate detection for species i and stage k at transect j during visit k
    for (k in 1:n_visits) {
    logit(p[j,k,i,l]) <- a0[i] + a1[i] * water[j, k]
    
    
    mu.p[j,k,i,l] <- p[j,k,i,l] * Z[j,i,l]
    y[j,k,i,l] ~ dbern(mu.p[j,k,i,l])
    
    }
    }
    }
    }
    
    ###### Derived quantities #######
    
    
    for(i in 1:n_species) {
      for(j in 1:n_trans) {
        occ_spp_stage[j, i] <- max(Z[ j, i, ])
    }
    occ_sp[i] <- sum(occ_spp_stage[ , i])        # Number of occupied transects by this species
    }

    for(j in 1:n_trans) {
      for(i in 1:n_species) {
        occ_trans_sp[j, i] <- max(Z[j, i, ])
      }
      occ_trans[j] <- sum(occ_trans_sp[j, ])      # Number of occurring species at each transect
    }

# mean detection by species and stage
for(i in 1:n_species) {
  for(l in 1:n_stages) {
    p_sp[i, l] <- sum(p[ , , i, l])
  }
}

# overall mean detection
    p_mean <- mean(p)


    }
    
